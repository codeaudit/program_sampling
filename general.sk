#define BOUND 143
bit[BOUND] tape = ??;
int tape_index = 0;

#define O_EQ 0
#define O_LT 1
#define O_GT 2
struct Guard {
  int z;
  int o;
}

bit run_guard(Guard g, int z) {
  if (g.o == O_EQ) return z == g.z;
  if (g.o == O_LT) return z <= g.z;
  if (g.o == O_GT) return z >= g.z;
  assert 0;
}

bit flip() {
  bit b = tape[tape_index];
  tape_index++;
  return b;
}

int random_integer() {
  //  return ??;
  int r = 0;
  int f = 1;
  for (int p = 0; p <= 2; p++) {
    r = r + flip()*f;
    f = f*2;
  }
  return r;
}


struct Array{
  int s;
  int [s] A;
}

 
bit array_equal(Array a,Array b) {
  if (a.s != b.s) return 0;
  for (int j = 0; j < a.s; j++)
    if (a.A[j] != b.A[j]) return 0;
  return 1;
}


Array concatenate(Array a1,Array a2) {
  Array a = new Array(s = a1.s + a2.s,A = a1.A);
  a.A[a1.s :: a2.s] = a2.A;
  return a;
}

Array concatenate3(Array a1,Array a2,Array a3) {
  assert a1 != null;
  assert a2 != null;
  assert a3 != null;
  /*  assert a1.s + a2.s + a3.s >= 0;*/
  int[a1.s + a2.s + a3.s] x;
  x[0 :: a1.s] = a1.A;
  x[a1.s :: a2.s] = a2.A;
  x[(a2.s+a1.s) :: a3.s] = a3.A;
  return new Array(s = a1.s + a2.s + a3.s,A = x);
}

Array cdr(Array a,ref bit success) {
  if (a.s == 0) {
    success = 0;
    return a;
  }
  success = 1;
  return new Array(s = a.s-1, A = a.A[1 :: (a.s-1)]);
}

Array filter(Guard g, Array a) {
  int outsz = 0;
  int [a.s] out;
  for (int i=0; i<a.s; ++i){
    if (run_guard(g,a.A[i])) {
      out[outsz++] = a.A[i];
    }
  }
  return new Array(s=outsz, A=out[0::outsz]);
}

Array singleton(int n) {
  return new Array(s = 1,A = {n});
}

int car(Array a,ref bit success) {
  success = 1;
  if (a.s > 0) return a.A[0];
  success = 0;
  return 0;
}

Array empty_list() {
  return new Array(s = 0,A = {});
}


Guard guard_expression(Array a,int d,ref bit success) {
  assert d > 1; // integer expression needs at least 1

  bit c1 = flip();
  bit c2 = flip();
  int o;
  if (c1) o = O_EQ;
  if (!c1 && c2) o = O_LT;
  if (!c1 && !c2) o = O_GT;
  
  bit zs;
  int z = integer_expression(a,d-1,zs);
  success = zs;

  return new Guard(z = z, o = o);
}

Array array_expression(Array a,int d,ref bit success) {
  assert d > 0;

  bit shallow = !(d > 2);

  bit c1 = flip();
  if (d == 1) { // only terminals allowed
    success = 1;
    if (c1)
      return empty_list();
    else
      return a;
  }
  

  bit c2 = flip();
  bit c3;
  if (!shallow) c3 = flip();

  bit array_success;
  Array lp = array_expression(a,d-1,array_success);
  bit integer_success;
  int z = integer_expression(a,d-1,integer_success);
  bit cdr_success;
  bit guard_success;
  Guard g;
  if (!shallow) g = guard_expression(a,d-1,guard_success);

  

  if ((shallow && c1 && c2) || (!shallow && c1 && c2 && c3)) {
    success = 1;
    return empty_list();
  }
  if ((shallow && c1 && !c2) || (!shallow && c1 && c2 && !c3)) {
    success = 1;
    return a;
  }
  if ((shallow && !c1 && c2) || (!shallow && c1 && !c2 && c3)) {
    if (array_success) {
      Array r = cdr(lp,cdr_success);
      success = cdr_success;
      return r;
    }
    success = 0;
    return empty_list();
  }
  if ((shallow && !c1 && !c2) || (!shallow && c1 && !c2 && !c3)) {
    success = integer_success;
    return singleton(z);
  }
  if (!shallow && !c1 && c2 && c3) {
    success = guard_success && array_success;
    return filter(g,lp);
  }

  assert 0;
}


  
int integer_expression(Array a,int d,ref bit success) {
  assert d > 0;
  if (d == 1) { success = 1; return 0; }

  bit c1 = flip();
  bit c2 = flip();
  bit c3 = flip();

  bit integer_success;
  int zp = integer_expression(a,d-1,integer_success);
  bit array_success;
  Array l = array_expression(a,d-1,array_success);

  //  int constant = random_integer();

  if (c1 && c2 && c3) { success = 1; return 0; }
  if (c1 && c2 && !c3) { success = integer_success; return zp+1; }
  if (c1 && !c2 && c3) { success = integer_success; return zp-1; }
  if (c1 && !c2 && !c3) {
    if (array_success) {
      bit car_success;
      int r = car(l,car_success);
      success = car_success;
      return r;
    }
    success = 0;
    return 0;
  }
  if (!c1 && c2 && c3) {
    success = array_success;
    return l.s;
  }
  //  if (!c1 && c2 && !c3) { success = 1; return constant; }
  assert 0;
}

Array recursive_expression(Array a, int bnd) {
  tape_index = 0;
  bit s1,s2,s3,s4,s5,s6;
  
  Guard g = guard_expression(a,3,s1);
  //  assert ?? == tape_index; // 6
  int target = integer_expression(a,3,s2);
  //  assert tape_index == 16; // 16
  Array b = array_expression(a,4,s3);


  assert s1 && s2 && s3;
  if (run_guard(g,target)) return b;

  if (bnd < 1) return null;

  Array x = array_expression(a,4,s4); bit rx = flip();
  Array y = array_expression(a,4,s5); bit ry = flip();
  Array z = array_expression(a,4,s6); bit rz = flip();


  
  //  assert tape_index == ??(10); // 47

  assert s4 && s5 && s6;

  //  if (rz || ry || rx) assert bnd > 0;
  if (rx) { assert x.s < a.s; x = recursive_expression(x, bnd-1); }
  if (ry) { assert y.s < a.s; y = recursive_expression(y, bnd-1); }
  if (rz) { assert z.s < a.s; z = recursive_expression(z, bnd-1); }

  

  return concatenate3(x,y,z);
}


Array specification(Array a) {
  if (a.s == 0) return empty_list();

  bit s;
  Guard g1 = new Guard(z = car(a,s), o = O_LT);
  Array x = filter(g1,a);
  Guard g2 = new Guard(z = car(a,s), o = O_GT);
  Array z = filter(g2,a);

  Array y = singleton(car(a,s));

  x = specification(x);
  z = specification(z);

  return concatenate3(x,y,z);
}


Array constant_array([int n],int [n] a) {
  return new Array(s = n,A = a);
}
void test_case([int n],int [n] a, int [n] b, int bnd) {
  Array o = recursive_expression(constant_array(a), bnd);

  assert o.s == n;
  assert o.A[0::n] == b;
}



/*
harness void l2() {
  test_case({1,2},{1,2},4);
  }
harness void l3() {
  test_case({4,5,3},{3,4,5},3);
}

harness void l4() {
  test_case({5,3,4},{3,4,5},4);
  }
*/

harness void l5() {
  test_case({5,4,3},{3,4,5},3);
}

harness void l6() {
  test_case({1,3,2},{1,2,3},3);
}


harness void l0() {
  test_case({},{},1);
}
  


/*
harness void pieces(int[4] a, int s) {
  assume s >= 0 && s < 5;
  Array x = new Array(s = s, A = a[0::s]);
  
  tape_index = 0;
  tape = {1, 1, 1, 1, 1, 1, 0, 0, 0, 1, 1, 1, 1, 1, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 0, 1, 1, 1, 1, 0, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 0, 0, 0, 0, 1, 0};

  bit u;
  Guard g = guard_expression(x,3,u);
  assert u;
  int target = integer_expression(x,3,u);
  assert u;

  assert run_guard(g,target) == (x.s == 0);

  Array b = array_expression(x,4,u);
  assert u;
  assert b.s == 0;

  if (x.s > 0) {
    Array p1 = array_expression(x,4,u);
    assert u;
    Guard g1 = new Guard(z = car(x,u), o = O_GT);
    assert u;
    Array p1p = filter(g1,x);
    assert array_equal(p1, p1p);

  }
}
*/
